# This script is meant to debug the SMARC RZ/G2L U-boot
# Please set the board boot mode to SCIF boot: SW11 ON-OFF-ON-OFF
# This script works only if OpenOCD write memory functions are patched:
# http://openocd.zylin.com/#/c/6266/

# Connect to OpenOCD
target remote localhost:3333
mon reset
mon halt
shell sleep 0.5

# Disable SMP
mon r9a07g044l.a55.0 aarch64 smp off
shell sleep 0.5

# Load BL2 code and use related symbols
l# You need to adjust the path to reflect yours
oad ~/repos/rzg_trusted-firmware-a/build/rzg2l/debug/bl2/bl2.elf
shell sleep 1
symbol-file ~/repos/rzg_trusted-firmware-a/build/rzg2l/debug/bl2/bl2.elf
shell sleep 1

# Restore BOOT_ID to the expected value: 0x3 = QSPI boot
mon mww 0x10000 0x3

# Insert HW breakpoint. SW breakpoints are failing because EL3 MMU is enabled
# and apparently memory is set to read only in the corresponding page
hbreak bl2_main
continue

# Set internal RAM to RO to force GDB using HW breakpoints
mem 0x10000 0x2FFFF ro

# Break at BL31 entry point
hbreak *0x44000000
shell sleep 1

# Add DDR memory region
mem 0x40000000 0x4FFFFFFF rw

continue
shell sleep 1
clear bl2_main

# Load BL31 code and related symbols
# You need to adjust the path to reflect yours
load ~/repos/rzg_trusted-firmware-a/build/rzg2l/debug/bl31/bl31.elf
shell sleep 1
symbol-file  ~/repos/rzg_trusted-firmware-a/build/rzg2l/debug/bl31/bl31.elf
shell sleep 1

hbreak bl31_main

# Set DDR BL31 region to RO to force GDB using HW breakpoints
delete mem 2
shell sleep 1
mem 0x40000000 0x4FFFFFFF ro

shell sleep 1
continue

# Break at U-boot entry point
hbreak *0x50000000
shell sleep 1

# Add DDR memory region
mem 0x50000000 0xBFFFFFFF rw

continue
shell sleep 1
clear bl31_main

# Load U-boot code (binary) and related symbols
# You need to adjust the path to reflect yours
# The load may take a while, ~ 50 seconds with J-link lite. Be patient.
#call memset(0x50000000, 0xCC, 0x10000)
restore ~/repos/uboot_g2/renesas-u-boot-cip/.out/u-boot.bin binary 0x50000000
shell sleep 1
symbol-file ~/repos/uboot_g2/renesas-u-boot-cip/.out/u-boot
shell sleep 1

# Set a Breakpoint to the start of the U-boot relocation routine
hb relocate_code
c

# Now we are ready to relocate U-boot symbols, note that "-o" requires a relatively new GDB (9.1 is OK).
# Do not forget to adjust the path to reflect yours also here
symbol-file ~/repos/uboot_g2/renesas-u-boot-cip/.out/u-boot -o gd->reloc_off

hb run_main_loop
c
